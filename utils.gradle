import groovy.util.XmlSlurper

def getVersionNameAdvanced() {
    // 下面这行代码中的 project 是谁？
    def xmlFile = project.file("src/main/AndroidManifest.xml")
    def rootManifest = new XmlSlurper().parse(xmlFile)
    return rootManifest['@package']
}

//对于 android library 编译，我会 disable 所有的 debug 编译任务
def disableDebugBuild(){
    //project.tasks 包含了所有的 tasks，下面的 findAll 是寻找那些名字中带 debug 的 Task。
    //返回值保存到 targetTasks 容器中
    def targetTasks = project.tasks.findAll{task ->
        task.name.contains("Debug")
    }
    //对满足条件的 task，设置它为 disable。如此这般，这个 Task 就不会被执行
    targetTasks.each{
        println "disable debug task : ${it.name}"
        it.setEnabled false
    }
}

//现在，想把这个 API 输出到各个 Project。由于这个 utils.gradle 会被每一个 Project Apply，所以
//我可以把 getVersionNameAdvanced 定义成一个 closure，然后赋值到一个外部属性
//下面的 ext 是谁的 ext？
ext { //此段花括号中代码是闭包
    //除了 ext.xxx=value 这种定义方法外，还可以使用 ext{}这种书写方法。
    //ext{}不是 ext(Closure)对应的函数调用。但是 ext{}中的{}确实是闭包。
    getVersionNameAdvanced = this.&getVersionNameAdvanced
    disableDebugBuild = this.&disableDebugBuild
    testParams = '1.0.0'
}
